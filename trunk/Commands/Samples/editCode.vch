# Programming in C-style languages

<newInsert> := New|Insert;

Statement Start = {Home} indentCodeLine();
<newInsert> Statement       =      addLine();
<newInsert> Statement Above = {Up} addLine();
<newInsert> Block        = newBlock();
<newInsert> Block 1..9   = newBlockOf($2);
<newInsert> Block 10..99 = newBlockAt($2);

<newInsert> Comment       =      addLine() "// ";
<newInsert> Comment Above = {Up} addLine() "// ";
Doc Comment Here  =      addLine() "/// ";
Doc Comment       =      addLine() "/// ";
Doc Comment Above = {Up} addLine() "/// ";
Comment   <n> [Lines] = selectLines($1) commentLines();
Uncomment <n> [Lines] = selectLines($1) uncommentLines();
Comment   That = commentLines();
Uncomment That = uncommentLines();
Comment Block 1..9   = {Home}{Enter}{Up} /* {Down_$1}{Down}{Home} */ {Enter};
Comment Block 10..99 = {Home}{Enter}{Up} /* gotoVisibleLine($1) {Down}{Home} */ {Enter};

insertMethod(qualifiers) := addLine() {Enter}{Up} code("$qualifiers void Do()") newBlock();
Insert Method = insertMethod(private);
[Insert] Method        (public|protected|private) = insertMethod($1);
[Insert] Method Static (public|protected|private) = insertMethod("static $1");

Insert For Loop   = addLine() code("for (int i = 0; i < n; i++)") newBlock();
Insert For Each   = addLine() code("foreach (var s in list)") newBlock();
<blockKeyword> := if|while|switch;
Insert <blockKeyword>         = addLine() code("$1 ()") {Left};
Insert <blockKeyword> 1..9    = saveCaret() newBlockOf($2)         restoreCaret() addLine() code("$1 ()") {Left};
Insert <blockKeyword> 10..99  = saveCaret() newBlockAt(Eval($2+1)) restoreCaret() addLine() code("$1 ()") {Left};
Insert Else       = addLine() code(else) addLine();
Insert Else Block = addLine() code(else) newBlock();
Insert Try        = addLine() code(try) newBlock() {Down}      addLine() code("catch (Exception ex)") newBlock() {Up_6} indentCodeLine();
Insert Try 1..9   = addLine() code(try) newBlockOf($1)         addLine() code("catch (Exception ex)") newBlock();
Insert Try 10..99 = addLine() code(try) newBlockAt(Eval($1+1)) addLine() code("catch (Exception ex)") newBlock();
